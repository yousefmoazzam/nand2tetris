// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // Potentially zero `x` input based on `zx` input
    And16(a=x, b=false, out=zeroedx);
    Mux16(a=x, b=zeroedx, sel=zx, out=zxres);

    // Potentially bitwise-negate `x` input based on `nx` input
    Not16(in=zxres, out=negatedx);
    Mux16(a=zxres, b=negatedx, sel=nx, out=nxres);

    // Potentially zero `y` input based on `zy` input
    And16(a=y, b=false, out=zeroedy);
    Mux16(a=y, b=zeroedy, sel=zy, out=zyres);

    // Potentially bitwise-negate `y` input based on `ny` input
    Not16(in=zyres, out=negatedy);
    Mux16(a=zyres, b=negatedy, sel=ny, out=nyres);

    // Compute the sum of the (potentially zeroed and/or bitwise-negated) `x`
    // and `y` inputs, in case it's needed when we later check the value of the
    // `f` input to see what computation should be put to the `out` pin
    Add16(a=nxres, b=nyres, out=sumnxresnyres);


    // Compute the bitwise AND of the (potentially zeroed and/or
    // bitwise-negated) `x` and `y` inputs, in case it's needed when we later
    // check the value of the `f` input to see what computation should be put
    // to the `out` pin
    And16(a=nxres, b=nyres, out=andnxresnyres);

    // Perform computation based on the value of `f` input
    Mux16(a=andnxresnyres, b=sumnxresnyres, sel=f, out=intermediateres);

    // Potentially bitwise-negate the output/result value
    Not16(in=intermediateres, out=negatedintermediateres);
    // Store:
    // - full result in internal pin as well as `out` pin in order to check if
    // it's zero or not
    // - most significant bit of result in internal pin in order to check if
    // it's negative or not
    Mux16(a=intermediateres, b=negatedintermediateres, sel=no, out=out, out=res, out[15]=resmsb);

    // Check if output value is zero
    And16(a=true, b=res, out[0..7]=isresultzerofirsthalf, out[8..15]=isresultzerosecondhalf);
    Or8Way(in=isresultzerofirsthalf, out=isresultfirsthalfallzero);
    Or8Way(in=isresultzerosecondhalf, out=isresultsecondhalfallzero);
    Or(a=isresultfirsthalfallzero, b=isresultsecondhalfallzero, out=isresultzero);
    Not(in=isresultzero, out=zr);

    // Check if output value is negative
    Mux(a=false, b=true, sel=resmsb, out=ng);
}
