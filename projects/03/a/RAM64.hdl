// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/RAM64.hdl
/**
 * Memory of sixty four 16-bit registers.
 * If load is asserted, the value of the register selected by
 * address is set to in; Otherwise, the value does not change.
 * The value of the selected register is emitted by out.
 */
CHIP RAM64 {
    IN in[16], load, address[6];
    OUT out[16];

    PARTS:
    // Set the load bit values for the 8 internal `RAM8` chips
    DMux8Way(in=load, sel=address[3..5], a=loadram0, b=loadram1, c=loadram2, d=loadram3, e=loadram4, f=loadram5, g=loadram6, h=loadram7);

    // Define the 8 internal `RAM8` chips
    RAM8(in=in, load=loadram0, address=address[0..2], out=outram0);
    RAM8(in=in, load=loadram1, address=address[0..2], out=outram1);
    RAM8(in=in, load=loadram2, address=address[0..2], out=outram2);
    RAM8(in=in, load=loadram3, address=address[0..2], out=outram3);
    RAM8(in=in, load=loadram4, address=address[0..2], out=outram4);
    RAM8(in=in, load=loadram5, address=address[0..2], out=outram5);
    RAM8(in=in, load=loadram6, address=address[0..2], out=outram6);
    RAM8(in=in, load=loadram7, address=address[0..2], out=outram7);

    // Pipe the output of the selected `RAM8` chip to the output pin
    Mux8Way16(a=outram0, b=outram1, c=outram2, d=outram3, e=outram4, f=outram5, g=outram6, h=outram7, sel=address[3..5], out=out);
}
