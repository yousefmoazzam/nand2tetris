// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/b/RAM512.hdl
/**
 * Memory of 512 16-bit registers.
 * If load is asserted, the value of the register selected by
 * address is set to in; Otherwise, the value does not change.
 * The value of the selected register is emitted by out.
 */
CHIP RAM512 {
    IN in[16], load, address[9];
    OUT out[16];

    PARTS:
    // Set the load bit values for the 8 internal `RAM64` chips
    DMux8Way(in=load, sel=address[6..8], a=loadram0, b=loadram1, c=loadram2, d=loadram3, e=loadram4, f=loadram5, g=loadram6, h=loadram7);

    // Define the 8 internal `RAM64` chips
    RAM64(in=in, load=loadram0, address=address[0..5], out=outram0);
    RAM64(in=in, load=loadram1, address=address[0..5], out=outram1);
    RAM64(in=in, load=loadram2, address=address[0..5], out=outram2);
    RAM64(in=in, load=loadram3, address=address[0..5], out=outram3);
    RAM64(in=in, load=loadram4, address=address[0..5], out=outram4);
    RAM64(in=in, load=loadram5, address=address[0..5], out=outram5);
    RAM64(in=in, load=loadram6, address=address[0..5], out=outram6);
    RAM64(in=in, load=loadram7, address=address[0..5], out=outram7);

    // Pipe the output of the selected `RAM64` chip to the output pin
    Mux8Way16(a=outram0, b=outram1, c=outram2, d=outram3, e=outram4, f=outram5, g=outram6, h=outram7, sel=address[6..8], out=out);
}
